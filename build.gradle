plugins {
    id 'groovy'
    id 'maven'
    id 'com.gradle.plugin-publish' version '0.9.1'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.1.5'
}

allprojects {
    apply plugin: 'net.minecrell.licenser'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'uk.jamierocks'
    version = '1.1.0-SNAPSHOT'
    description = 'A Gradle plugin for running Sponge plugins from within your IDE, without complicated setup procedures.'

    repositories {
        mavenCentral()
    }

    license {
        header rootProject.file('HEADER.txt')
        include '**/*.java'
        include '**/*.groovy'
    }
}

configurations {
    library
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    library project(':LaunchTool')
}

jar {
    into('lib') {
        from configurations.library
    }
}

pluginBundle {
    website = 'https://github.com/jamierocks/KitchenSink'
    vcsUrl = 'https://github.com/jamierocks/KitchenSink'
    description = project.description
    tags = ['sponge', 'minecraft']

    plugins {
        kitchensinkPlugin {
            id = 'uk.jamierocks.kitchensink'
            displayName = 'KitchenSink'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}